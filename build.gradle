plugins {
  id("se.patrikerdes.use-latest-versions") version "0.2.13"
  id("com.github.ben-manes.versions") version "0.27.0"
  id("java-gradle-plugin")
  id "com.gradle.plugin-publish" version "0.11.0"
}

description = 'Gradle plugin that combines git tags and semantic versioning, and sets the gradle version property accordingly.'

apply plugin: "com.gradle.plugin-publish"

apply plugin: 'groovy'
sourceCompatibility = '1.7'

dependencies {
  compile gradleApi()
  compile localGroovy()
}

apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'wrapper'

wrapper {
  gradleVersion = "5.0"
  distributionType = Wrapper.DistributionType.ALL
}

repositories {
  mavenCentral()
}

dependencies {
  testCompile 'junit:junit:4.12'
}

// the following sets the version property
plugins.apply com.cinnober.gradle.semver_git.SemverGitPlugin

group = 'com.cinnober.gradle'

// -- Maven Central --

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

// -- Gradle Plugin Portal --

gradlePlugin {
  plugins {
    semver {
      id = "com.cinnober.gradle.semver-git"
      implementationClass = "com.cinnober.gradle.semver_git.SemverGitPlugin"
      displayName = "semver-git"
    }
  }
}

pluginBundle {
  website = 'https://github.com/cinnober/semver-git'
  vcsUrl = 'https://github.com/cinnober/semver-git'
  description = project.description
  tags = ['git', 'semantic-versioning', 'semver']
}

publishing {
  repositories {
    maven {
      def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
      url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

      credentials {
        username ossrhUsername
        password ossrhPassword
      }
    }
  }
}

publishing.publications.whenObjectAdded {
  it.pom {
    url = 'https://github.com/cinnober/semver-git'
    description = project.description

    scm {
      connection = 'scm:git:https://github.com/cinnober/semver-git'
      developerConnection = 'scm:git:https://github.com/cinnober/semver-git'
      url = 'https://github.com/cinnober/semver-git'
    }

    licenses {
      license {
        name = 'MIT License'
        url = 'http://www.opensource.org/licenses/mit-license.php'
      }
    }

    developers {
      developer {
        id = 'mikael.brannstrom'
        name = 'Mikael Brännström'
        email = 'mikael.brannstrom@cinnober.com'
      }
      developer {
        id = 'deepy'
        name = 'Alex Nordlund'
        email = 'alexander.nordlund@cinnober.com'
      }
    }
  }
}
